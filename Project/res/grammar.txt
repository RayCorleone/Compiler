<PROGRAM>::=<VARS>
<VARS>::=<VAR>|<VAR><VARS>
<VAR>::='INT''ID'<VARTYPE>';'|<FUNC><SENBLOCK>
<VARTYPE>::='none'|<ARRAYVAR>
<ARRAYVAR>::='[''NUM'']'|'[''NUM'']'<ARRAYVAR>
<FUNC>::='VOID''ID''('<PARAM>')'|'INT''ID''('<PARAM>')'
<PARAM>::=<PARAMLIST>|'VOID'|'none'
<PARAMLIST>::='INT''ID'|'INT''ID'','<PARAMLIST>
<SENBLOCK>::='{'<SENSEQ>'}'
<SENSEQ>::=<SENTENCE>|<SENTENCE><SENSEQ>
<SENTENCE>::=<IFSEN>|<WHILESEN>|<RETSEN>';'|<ASSIGNMENT>';'|<INVARDEF>';'
<INVARDEF>::='INT''ID'|'INT''ID'<ARRAYVAR>
<WHILESEN>::='WHILE''('<CTRL>')'<SENBLOCK>
<IFSEN>::='IF''('<CTRL>')'<SENBLOCK>|'IF''('<CTRL>')'<SENBLOCK>'ELSE'<SENBLOCK>
<RETSEN>::='RET'<EXPRESSION>|'RET'
<ASSIGNMENT>::='ID''ASSIGN'<EXPRESSION>|<ARRAY>'ASSIGN'<EXPRESSION>
<CTRL>::=<EXPRESSION>
<EXPRESSION>::=<BAND>|<BAND>'OR'<EXPRESSION>
<BAND>::=<BNOT>|<BNOT>'AND'<BAND>
<BNOT>::=<COMP>|'NOT'<COMP>
<COMP>::=<PLUSEX>|<PLUSEX>'CMP'<COMP>
<PLUSEX>::=<TERM>|<TERM>'OP1'<PLUSEX>
<TERM>::=<FACTOR>|<FACTOR>'OP2'<TERM>
<FACTOR>::='NUM'|'('<EXPRESSION>')'|'ID'|<ARRAY>|'ID'<CALL>|'('<ASSIGNMENT>')'
<CALL>::='('<ACTUALPARAM>')'
<ARRAY>::='ID''['<EXPRESSION>']'|<ARRAY>'['<EXPRESSION>']'
<ACTUALPARAM>::=<ACTUALPARAMLIST>|'none'
<ACTUALPARAMLIST>::=<EXPRESSION>|<EXPRESSION>','<ACTUALPARAMLIST>